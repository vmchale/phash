{-# LANGUAGE ScopedTypeVariables #-}

module Parallel ( pathMaps ) where

import           Control.Concurrent.STM      (atomically)
import           Control.Concurrent.STM.TVar (TVar, modifyTVar', newTVarIO, readTVarIO)
import           Data.Functor                (($>))
import           Data.List.NonEmpty          (NonEmpty (..), (<|))
import qualified Data.Map                    as M
import           Data.Word                   (Word64)
import           PerceptualHash              (fileHash)
import           System.Directory.Parallel   (parTraverseAll)
import           System.FilePath             (takeExtension)
import           System.IO                   (hPutStrLn, stderr)

imgExtension :: String -> Bool
imgExtension ".jpg"  = True
imgExtension ".JPG"  = True
imgExtension ".jpeg" = True
imgExtension ".png"  = True
imgExtension ".gif"  = True
imgExtension ".hdr"  = True
imgExtension ".pic"  = True
imgExtension ".bmp"  = True
imgExtension ".TGA"  = True
imgExtension ".tga"  = True
imgExtension ".tif"  = True
imgExtension ".tiff" = True
#ifdef WEBP
imgExtension ".webp" = True
#endif
#ifdef AVIF
imgExtension ".avif" = True
#endif
imgExtension _       = False

insertHash :: FilePath -> IO (M.Map Word64 (NonEmpty (FilePath, (Int, Int))) -> M.Map Word64 (NonEmpty (FilePath, (Int, Int))))
insertHash fp = do
    res <- fileHash fp
    case res of
        Right (x, d) ->
            pure $ \hashes ->
                let go (Just others) = Just ((fp, d) <| others)
                    go Nothing       = Just ((fp, d) :| [])
                    in M.alter go x hashes
        Left err -> hPutStrLn stderr ("WARNING: skipping " ++ fp ++ "\n" ++ err) $> id

stepMap :: TVar (M.Map Word64 (NonEmpty (FilePath, (Int, Int)))) -> FilePath -> IO ()
stepMap var fp = do
    mod' <- insertHash fp
    atomically $ modifyTVar' var mod'

pathMaps :: [FilePath] -> [FilePath] -> IO (M.Map Word64 (NonEmpty (FilePath, (Int, Int))))
pathMaps fps excls = do
    total <- newTVarIO mempty
    parTraverseAll (stepMap total) fileFilter (\fp -> pure (fp `notElem` excls)) fps
    readTVarIO total

    where fileFilter = pure . imgExtension . takeExtension
